buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "1"
group= "uk.co.haxyshideout.ChocoCraft2" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ChocoCraft2"

idea { module { inheritOutputDirs = true } }

configurations {
    shade
    compile.extendsFrom shade
}

minecraft {
    version = "1.8-11.14.3.1474"
    mappings = "snapshot_20150622"
    srgExtra "PK: ninja/leaping uk/co/haxyshideout/shaded/ninja/leaping"
}

repositories {
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    shade 'ninja.leaping.configurate:configurate-hocon:2.0.1-SNAPSHOT'
    compile 'org.projectlombok:lombok:1.16.6'
}

processResources
{

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'fversion':"Forge 10.13.2.1230;Forge 10.13.0.1208;Forge 10.13.0.1180"
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    
    from(sourceSets.main.allSource) {
        include '**/*.data'
        include '**/*.schematic'
    }
    
}

// this sets our output jar to have a 'tag' of 'universal' on it
// It also adds the minecraft version in a custom version name
// The result is files named <projectname>-<mcversion>-<version>-universal.jar
jar {
    includeEmptyDirs = false
    classifier = 'universal'
    version = "${project.minecraft.version}-${project.version}"
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

task sourceJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'src'
    version = "${project.minecraft.version}-${project.version}"
}

// Add in an mcp named jar, for those who wish to run in a development environment (assuming mcp naming matches)
task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
    version = "${project.minecraft.version}-${project.version}"
}


artifacts{
  archives sourceJar, deobfJar
}
